<html>
    <head>
	<title>W05: Task</title>
    </head>
    <body>
	<script src="https://threejs.org/build/three.min.js"></script>

	<script type="x-shader/x-vertex" id="gouraud.vert">
	 varying vec3 point_color;
	 varying vec4 point_position;
	 varying vec3 normal_vector;
	 uniform vec3 light_position;
	 uniform vec3 camera_position;

	 vec3 LambertianReflection( vec3 C, vec3 L, vec3 N )
	 {
	     float ka = 0.3;
	     float kd = 0.5;

	     float dd = dot( N, L );
	     float Ia = ka;
	     float Id = kd * dd;
             return C * ( Ia + Id );
	 }

	 vec3 PhongReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float n = 50.0;

	     vec3 R = reflect( -L, N );
	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 ds = pow( dot( R, V ), n );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 vec3 BlinnPhongReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float n = 50.0;

	     vec3 H = normalize( L + V );
	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 ds = pow( dot( H, N ), n );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 vec3 CookTorranceReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float m = 0.2; // roughness. [0:smooth, 1:rough]
	     float F0 = 0.2;

	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 vec3 H = normalize( L + V );
		 float NL = dd;
		 float NH = dot( N, H );
		 float NV = dot( N, V );
		 float VH = dot( V, H );

		 // Beckmann distribution
		 float NH2 = NH * NH;
		 float NH4 = NH2 * NH2;
		 float D = ( 1.0 / ( 4.0 * m * m * NH4 ) ) * exp( ( NH2 - 1.0 ) / ( m * m * NH2 ) );

		 // Geometrical attenuation factor
		 float G = min( 1.0, min( 2.0 * NH * NV / VH, 2.0 * NH * NL / VH ) );

		 // Fresnel term
		 float n = ( 1.0 + sqrt( F0 ) ) / ( 1.0 - sqrt( F0 ) );
		 float c = VH;
		 float g = sqrt( n * n + c * c - 1.0 );
		 float gc0 = g - c;
		 float gc1 = g + c;
		 float cgc0 = c * gc1 - 1.0;
		 float cgc1 = c * gc0 + 1.0;
		 float f0 = ( gc0 * gc0 ) / ( gc1 * gc1 );
		 float f1 = 1.0 + ( cgc0 / cgc1 ) * ( cgc0 / cgc1 );
		 float F = 0.5 * f0 * f1;

		 // Specular component
		 ds = ( F / 3.14159265 ) * ( D * G / ( NL * NV ) );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 void main()
	 {
	     point_position = modelViewMatrix * vec4( position, 1.0 );
	     normal_vector = normalMatrix * normal;

	     vec3 C = color;
	     vec3 L = normalize( light_position - point_position.xyz );
	     vec3 N = normalize( normal_vector );
	     vec3 V = normalize( camera_position - point_position.xyz );

	     #if defined( Lambert )
	     point_color = LambertianReflection( C, L, N );
	     #elif defined( Phong )
	     point_color = PhongReflection( C, L, N, V );
	     #elif defined( BlinnPhong )
	     point_color = BlinnPhongReflection( C, L, N, V );
	     #elif defined( CookTrrance )
	     point_color = CookTorranceReflection( C, L, N, V );
	     #else
	     point_color = C;
	     #endif

	     gl_Position = projectionMatrix * point_position;
	 }
	</script>

	<script type="x-shader/x-fragment" id="gouraud.frag">
	 varying vec3 point_color;

	 void main()
	 {
	     gl_FragColor = vec4( point_color, 1.0 );
	 }
	</script>

	<script type="x-shader/x-vertex" id="phong.vert">
	 varying vec3 point_color;
	 varying vec4 point_position;
	 varying vec3 normal_vector;

	 void main()
	 {
	     point_color = color;
	     point_position = modelViewMatrix * vec4( position, 1.0 );
	     normal_vector = normalMatrix * normal;

	     gl_Position = projectionMatrix * point_position;
	 }
	</script>

	<script type="x-shader/x-fragment" id="phong.frag">
	 varying vec3 point_color;
	 varying vec4 point_position;
	 varying vec3 normal_vector;
	 uniform vec3 light_position;
	 uniform vec3 camera_position;
	 uniform int reflection_model;

	 vec3 LambertianReflection( vec3 C, vec3 L, vec3 N )
	 {
	     float ka = 0.3;
	     float kd = 0.5;

	     float dd = dot( N, L );
	     float Ia = ka;
	     float Id = kd * dd;
             return C * ( Ia + Id );
	 }

	 vec3 PhongReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float n = 50.0;

	     vec3 R = reflect( -L, N );
	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 ds = pow( dot( R, V ), n );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 vec3 BlinnPhongReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float n = 50.0;

	     vec3 H = normalize( L + V );
	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 ds = pow( dot( H, N ), n );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 vec3 CookTorranceReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float m = 0.2; // roughness. [0:smooth, 1:rough]
	     float F0 = 0.2;

	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 vec3 H = normalize( L + V );
		 float NL = dd;
		 float NH = dot( N, H );
		 float NV = dot( N, V );
		 float VH = dot( V, H );

		 // Beckmann distribution
		 float NH2 = NH * NH;
		 float NH4 = NH2 * NH2;
		 float D = ( 1.0 / ( 4.0 * m * m * NH4 ) ) * exp( ( NH2 - 1.0 ) / ( m * m * NH2 ) );

		 // Geometrical attenuation factor
		 float G = min( 1.0, min( 2.0 * NH * NV / VH, 2.0 * NH * NL / VH ) );

		 // Fresnel term
		 float n = ( 1.0 + sqrt( F0 ) ) / ( 1.0 - sqrt( F0 ) );
		 float c = VH;
		 float g = sqrt( n * n + c * c - 1.0 );
		 float gc0 = g - c;
		 float gc1 = g + c;
		 float cgc0 = c * gc1 - 1.0;
		 float cgc1 = c * gc0 + 1.0;
		 float f0 = ( gc0 * gc0 ) / ( gc1 * gc1 );
		 float f1 = 1.0 + ( cgc0 / cgc1 ) * ( cgc0 / cgc1 );
		 float F = 0.5 * f0 * f1;

		 // Specular component
		 ds = ( F / 3.14159265 ) * ( D * G / ( NL * NV ) );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 void main()
	 {
	     vec3 C = point_color;
	     vec3 L = normalize( light_position - point_position.xyz );
	     vec3 N = normalize( normal_vector );
	     vec3 V = normalize( camera_position - point_position.xyz );

	     #if defined( Lambert )
	     vec3 shaded_color = LambertianReflection( C, L, N );
	     #elif defined( Phong )
	     vec3 shaded_color = PhongReflection( C, L, N, V );
	     #elif defined( BlinnPhong )
	     vec3 shaded_color = BlinnPhongReflection( C, L, N, V );
	     #elif defined( CookTrrance )
	     vec3 shaded_color = CookTorranceReflection( C, L, N, V );
	     #else
	     vec3 shaded_color = C;
	     #endif

	     gl_FragColor = vec4( shaded_color, 1.0 );
	 }
	</script>
	<script src="w05_task.js"></script>

	<div style="width:1200px;">
	    <div>
		<div style="width:12%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3"></font></label></div>
		<div style="width:22%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3">Lambertian Reflection</font></label></div>
		<div style="width:22%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3">Phong Reflection</font></label></div>
		<div style="width:22%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3">Blinn-Phong Reflection</font></label></div>
		<div style="width:22%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3">Cook-Trrance Reflection</font></label></div>
	    </div>
	    <div>
		<div style="width:12%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3">Gouraud Shading</font></label></div>
		<div id="display1" style="width:22%; float:left; margin-bottom: 5px;"></div>
		<div id="display2" style="width:22%; float:left; margin-bottom: 5px;"></div>
		<div id="display3" style="width:22%; float:left; margin-bottom: 5px;"></div>
		<div id="display4" style="width:22%; float:left; margin-bottom: 5px;"></div>
	    </div>
	    <div>
		<div style="width:12%; float:left; margin-bottom: 5px;"><label style="font-family:Arial;"><font size="3">Phong Shading</font></label></div>
		<div id="display5" style="width:22%; float:left; margin-bottom: 5px;"></div>
		<div id="display6" style="width:22%; float:left; margin-bottom: 5px;"></div>
		<div id="display7" style="width:22%; float:left; margin-bottom: 5px;"></div>
		<div id="display8" style="width:22%; float:left; margin-bottom: 5px;"></div>
	    </div>
	</div>

	<script>
	 var vert_shader = document.getElementById('gouraud.vert').text;
	 var frag_shader = document.getElementById('gouraud.frag').text;
	 var reflection_model = "Lambert";
	 var target_dom = document.getElementById('display1');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

	<script>
	 var vert_shader = document.getElementById('gouraud.vert').text;
	 var frag_shader = document.getElementById('gouraud.frag').text;
	 var reflection_model = "Phong";
	 var target_dom = document.getElementById('display2');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

	<script>
	 var vert_shader = document.getElementById('gouraud.vert').text;
	 var frag_shader = document.getElementById('gouraud.frag').text;
	 var reflection_model = "BlinnPhong";
	 var target_dom = document.getElementById('display3');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

	<script>
	 var vert_shader = document.getElementById('gouraud.vert').text;
	 var frag_shader = document.getElementById('gouraud.frag').text;
	 var reflection_model = "CookTrrance";
	 var target_dom = document.getElementById('display4');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>


	<script>
	 var vert_shader = document.getElementById('phong.vert').text;
	 var frag_shader = document.getElementById('phong.frag').text;
	 var reflection_model = "Lambert";
	 var target_dom = document.getElementById('display5');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

	<script>
	 var vert_shader = document.getElementById('phong.vert').text;
	 var frag_shader = document.getElementById('phong.frag').text;
	 var reflection_model = "Phong";
	 var target_dom = document.getElementById('display6');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

	<script>
	 var vert_shader = document.getElementById('phong.vert').text;
	 var frag_shader = document.getElementById('phong.frag').text;
	 var reflection_model = "BlinnPhong";
	 var target_dom = document.getElementById('display7');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

	<script>
	 var vert_shader = document.getElementById('phong.vert').text;
	 var frag_shader = document.getElementById('phong.frag').text;
	 var reflection_model = "CookTrrance";
	 var target_dom = document.getElementById('display8');
	 main( vert_shader, frag_shader, reflection_model, target_dom );
	</script>

    </body>
</html>
